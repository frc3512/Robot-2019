plugins {
    id "cpp"
    id "google-test-test-suite"
    id "edu.wpi.first.GradleRIO" version "2020.3.2"
    id "org.ysb33r.doxygen" version "0.5"
}

task pythonTask(type: Exec) {
    workingDir '.'
    commandLine 'python3', 'python/generate_messages.py', '--input', 'msgs', '--output', 'build/generated'
}

// Define my targets (RoboRIO) and artifacts (deployable files)
// This is added by GradleRIO's backing project EmbeddedTools.
deploy {
    targets {
        roboRIO("roborio") {
            // Team number is loaded either from the .wpilib/wpilib_preferences.json
            // or from command line. If not found an exception will be thrown.
            // You can use getTeamOrDefault(team) instead of getTeamNumber if you
            // want to store a team number in this file.
            team = frc.getTeamOrDefault(3512)
        }
    }
    artifacts {
        frcNativeArtifact('frcCpp') {
            targets << "roborio"
            component = 'frcUserProgram'
            // Debug can be overridden by command line, for use with VSCode
            debug = frc.getDebugOrDefault(false)
        }
        // Built in artifact to deploy arbitrary files to the roboRIO.
        fileTreeArtifact('frcStaticFileDeploy') {
            // The directory below is the local directory to deploy
            files = fileTree(dir: 'src/main/deploy')
            // Deploy to RoboRIO target, into /home/lvuser/deploy
            targets << "roborio"
            directory = '/home/lvuser/deploy'
        }
    }
}

// Set this to true to enable desktop support.
def includeDesktopSupport = true

// Enable simulation gui support. Must check the box in vscode to enable support
// upon debugging
dependencies {
    simulation wpi.deps.sim.gui(wpi.platforms.desktop, true)
}

nativeUtils.platformConfigs.named("linuxathena").configure {
    it.cppCompiler.args.remove('-g')
}

nativeUtils.platformConfigs.named("linuxx86-64").configure {
    it.cppCompiler.args.remove('-g')
    it.linker.args.add('-lstdc++fs')
}

model {
    components {
        frcUserProgram(NativeExecutableSpec) {
            targetPlatform wpi.platforms.roborio
            if (includeDesktopSupport) {
                targetPlatform wpi.platforms.desktop
            }

            binaries {
              all {
                if (it.buildType.name.contains('debug')) {
                  it.buildable = false
                }
              }
            }

            binaries.all {
                binary ->
                binary.getTasks().withType(AbstractNativeSourceCompileTask) {
                    it.dependsOn pythonTask
                }
            }

            sources.cpp {
                source {
                    srcDirs = ['src/main/cpp', 'thirdparty/cpp', 'build/generated/cpp']
                    include '**/*.cpp', '**/*.cc'
                }
                exportedHeaders {
                    srcDirs = ['src/main/include', 'thirdparty/include', 'build/generated/include']
                }
            }

            // Defining my dependencies. In this case, WPILib (+ friends), and vendor libraries.
            wpi.deps.vendor.cpp(it)
            wpi.deps.wpilib(it)
        }
    }
    testSuites {
        frcUserProgramTest(GoogleTestTestSuiteSpec) {
            testing $.components.frcUserProgram

            binaries {
              all {
                if (it.buildType.name.contains('debug')) {
                  it.buildable = false
                }
              }
            }

            binaries.all {
                binary ->
                binary.getTasks().withType(AbstractNativeSourceCompileTask) {
                    it.dependsOn pythonTask
                }
            }

            sources.cpp {
                source {
                    srcDir 'src/test/cpp'
                    include '**/*.cpp'
                    include '**/*.cc'
                }

                exportedHeaders {
                    srcDirs += ['src/test/include', 'src/test/include/frc']
                }
            }

            wpi.deps.vendor.cpp(it)
            wpi.deps.wpilib(it)
            wpi.deps.googleTest(it)
        }
    }
}

task buildAthena {
    dependsOn 'frcUserProgramLinuxathenaReleaseExecutable'
}

task removeLogs(type: Exec) {
    workingDir '.'
    commandLine './buildscripts/remove-logs.sh'
}

task test {
    dependsOn 'removeLogs'
    dependsOn 'runFrcUserProgramTestLinuxx86-64ReleaseGoogleTestExe'
}

task simulate {
    dependsOn 'removeLogs'
    dependsOn 'simulateFrcUserProgramLinuxx86-64DebugExecutable'
}

doxygen {
    executables {
        doxygen version : '1.8.18',
            baseURI : 'https://frcmaven.wpi.edu/artifactory/generic-release-mirror/doxygen'
    }

    generate_html true
    extension_mapping 'inc=C++'
    project_name 'Robot-2019'
    quiet true
    warn_if_undocumented false
    generate_latex false
    use_mathjax true
    html_timestamp true
    generate_treeview true

    source 'src/main/include'
    outputDir 'docs/'
}
